buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.batcha.gradle.plugins:git-dependencies:0.1'
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jpsg' // defined in buildSrc

    sourceCompatibility = 1.6

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.kohsuke.jetbrains:annotations:9.0'
        testCompile 'junit:junit:4.11'
    }

    tasks.withType(net.openhft.jpsg.GeneratorTask).each { gen ->
        gen.addProcessor net.openhft.jpsg.collect.MethodGeneratingProcessor
    }

    idea.module {
        excludeDirs = [file('.gradle')]
        ['classes', 'docs', 'dependency-cache', 'libs', 'reports', 'resources', 'test-results',
                'tmp'].each { excludeDirs << file("$buildDir/$it") }
    }
}

project(':api').javadoc.options.links 'http://docs.oracle.com/javase/7/docs/api/'

project(':impl') {
    apply plugin: 'git-dependencies'

    dependencies {
        compile project(':api')
        testCompile('com.google.guava:guava-testlib:17.0-SNAPSHOT') {
            ext.git = 'https://code.google.com/p/guava-libraries/'
        }
        testCompile 'org.apache.mahout:mahout-math:0.9'
    }

    test {
        jvmArgs '-XX:MaxJavaStackTraceDepth=-1'
    }
}