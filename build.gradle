task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

allprojects {
    apply plugin: 'idea'

    task idea(overwrite: true) {
        dependsOn ideaModule
    }

    task cleanIdea(overwrite: true) {
        dependsOn cleanIdeaModule
    }
}

def leafProjects = subprojects.findAll { it.subprojects.empty }
configure(leafProjects) {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.google.code.findbugs:jsr305:2.0.3'
        testCompile 'junit:junit:4.11'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }

    configure(tasks.withType(AbstractCompile)) {
        options.encoding = 'UTF-8'
    }

    configure(tasks.withType(JavaCompile)) {
        options.compilerArgs << '-Xlint:all'
    }

    apply from: "$rootProject.projectDir/methods.gradle"

    setSourceCompatibility(project, 1.8)
}

def metaProjects = [':jpsg:core', ':jpsg:gradle-plugin', ':lib:impl-generator']
        .collect(this.&project)
metaProjects.each { p -> evaluationDependsOn(p.path) }
task buildMeta { dependsOn metaProjects*.build }
task cleanMeta { dependsOn metaProjects*.clean }

def mainProjects = leafProjects - metaProjects - project(':jpsg').subprojects
configure(mainProjects) {
    task buildMain { dependsOn build }
    task cleanMain { dependsOn clean }
}

tasks.idea.dependsOn ideaProject
tasks.idea << {
    copy {
        from '.'
        into '.idea/'
        include '*.ipr'
        rename { 'modules.xml' }
    }
    cleanIdeaProject.execute()
}

cleanIdea << {
    delete '.idea/modules.xml'
}
