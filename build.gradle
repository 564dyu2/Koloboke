buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath 'org.batcha.gradle.plugins:git-dependencies:0.1'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.5'
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jpsg' // defined in buildSrc
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-idea'

    sourceCompatibility = 1.6

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.google.code.findbugs:jsr305:2.0.3'
        testCompile 'junit:junit:4.11'
        provided 'com.google.auto.value:auto-value:1.0-rc1'
    }

    tasks.withType(net.openhft.jpsg.GeneratorTask).each { gen ->
        gen.addProcessor net.openhft.jpsg.collect.MethodGeneratingProcessor
    }

    idea.module {
        excludeDirs = [file('.gradle')]
        ['docs', 'dependency-cache', 'libs', 'reports', 'resources', 'test-results', 'tmp']
                .each { excludeDirs << file("$buildDir/$it") }
    }
}

project(':api').javadoc.options.links 'http://docs.oracle.com/javase/7/docs/api/'

project(':impl') {
    apply plugin: 'git-dependencies'

    dependencies {
        compile project(':api')
        testCompile('com.google.guava:guava-testlib:17.0-SNAPSHOT') {
            ext.git = 'https://code.google.com/p/guava-libraries/'
        }
        testCompile 'org.apache.mahout:mahout-math:0.9'
    }

    test {
        jvmArgs '-XX:MaxJavaStackTraceDepth=-1'
    }
}