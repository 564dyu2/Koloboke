buildscript {
    repositories {
    	mavenCentral()
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
	dependencies {
		classpath project(':jpsg:gradle-plugin')
        classpath project('impl-generator')
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
	}
}

configure([project('api'), project('impl')]) {
    applyJpsg()
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-idea'
    apply plugin: 'findbugs'

	setSourceCompatibility(project, 1.6)

    dependencies {
        provided 'com.google.auto.value:auto-value:1.0-rc1'
        findbugs fileTree("$parent.projectDir/findbugs/libs/").include('*.jar')
    }

    compileJava {
        // To suppress warnings about usages of sun.misc.Unsafe API
        options.compilerArgs << '-XDignore.symbol.file'
        options.compilerArgs += ['-Xlint:-unchecked', '-Xlint:-rawtypes',
                                 '-Xlint:-cast', '-Xlint:-deprecation']
        options.fork = true
        def javac = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'
        options.forkOptions.executable = javac
        
    }

    findbugs {
        effort = 'max'
        reportLevel = 'high'
        excludeFilter = file("$parent.projectDir/findbugs/config/excludeFilter.xml")
    }

    idea.module {
        excludeDirs.remove(file("$buildDir/classes"))
        // AutoValue implementations are located there
        sourceDirs += sourceSets*.output.classesDir
    }
}