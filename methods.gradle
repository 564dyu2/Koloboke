def setSourceCompatibility(project, level) { configure(project) {
    sourceCompatibility = level
    def levelAsString = level.toString()
    def langLevel = 'JDK_' + levelAsString.replace('.', '_')
    idea.module.iml { withXml {
        it.asNode().component.find {
            it.@name == 'NewModuleRootManager'
        }.@LANGUAGE_LEVEL = langLevel
    } }
    project.beforeEvaluate {
        if (ext.has('jpsgApplied')) {
            generateJavaSpecializations {
                with 'jdk=JDK' + levelAsString.substring(levelAsString.length() - 1)
            }
        }
    }
} }

def applyJpsg() {
    apply plugin: 'jpsg'
    idea.module {
        excludeDirs = [file('.gradle')]
        ['classes', 'dependency-cache', 'docs', 'java-compiler', 'libs', 'reports', 'resources',
         'test-results', 'tmp'].each { excludeDirs << file("$buildDir/$it") }
    }

    ext.jpsgApplied = true
}

ext.setSourceCompatibility = this.&setSourceCompatibility
ext.applyJpsg = this.&applyJpsg